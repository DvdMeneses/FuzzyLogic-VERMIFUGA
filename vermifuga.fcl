/*
	Example: A tip calculation FIS (fuzzy inference system)
	Calculates tip based on 'service' and 'food'

*/

FUNCTION_BLOCK vermifuga	// Block definition (there may be more than one block per file)

VAR_INPUT				// Define input variables
	famacha : REAL;
	opg : REAL;
END_VAR

VAR_OUTPUT				// Define output variable
	situacaoAnimal : REAL;
END_VAR

FUZZIFY famacha			// Fuzzify input variable 'famacha': {'begeClaro', 'bege' , 'laranja', 'laranjaEscuro' , 'vermelho'}
	TERM begeClaro := (0, 1) (2, 0) ; 
	TERM bege := (1.5, 0) (2,1) (3,0);
	TERM laranja := (2.5, 0) (3, 1) (3.5,0);
	TERM laranjaEscuro := (3,0) (3,1) (4.5,0);
	TERM vermelho := (4,0) (4.5,1) (5,1);
END_FUZZIFY

FUZZIFY opg			// Fuzzify input variable 'OPG ': { 'baixo', 'medido', 'alto' }
	TERM baixo := (0, 1) (200, 1) (400,0) ;
	TERM medio := (300,0) (400,1) (500,0);
	TERM alto := (400,0) (500,1) (600,1);
END_FUZZIFY

DEFUZZIFY situacaoAnimal			// Defuzzify output variable 'situacaoAnimal' : {'muitoMal', 'mal', 'atencao','saudavel' }
	TERM muitoMal := (0,1) (10,1) (14.9,0);
	TERM mal := (15,0) (20,1) (24.9,0);
	TERM atencao := (25,0) (30,1) (34.9,0);
	TERM saudavel := (35,0) (40,1) (50,1);
	METHOD : COG;		// Use 'Center Of Gravity' defuzzification method
	DEFAULT := 0;		// Default value is 0 (if no rule activates defuzzifier)
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' 
						// (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method
	
	
	RULE 1 : IF famacha IS vermelho AND opg IS  alto THEN situacaoAnimal IS mal;
	RULE 2 : IF famacha IS vermelho AND opg is medio THEN situacaoAnimal IS saudavel;
	RULE 3 : IF famacha IS vermelho AND opg IS  baixo THEN situacaoAnimal IS saudavel;	
	
	RULE 4 : IF famacha IS laranjaEscuro AND opg IS  alto THEN situacaoAnimal IS mal;
	RULE 5 : IF famacha IS laranjaEscuro AND opg is medio THEN situacaoAnimal IS atencao;
	RULE 6 : IF famacha IS laranjaEscuro AND opg IS  baixo THEN situacaoAnimal IS saudavel;
	
	 
	RULE 7 : IF famacha IS laranja AND opg IS medio THEN situacaoAnimal IS atencao;
	RULE 8 : IF famacha IS laranja AND opg IS alto THEN situacaoAnimal IS mal;
	RULE 9 : IF famacha IS laranja AND opg IS baixo THEN situacaoAnimal IS saudavel;
	
	RULE 10 : IF famacha IS bege AND opg IS medio THEN situacaoAnimal IS mal;
	RULE 11: IF famacha IS bege AND opg IS alto THEN situacaoAnimal IS muitoMal;
	RULE 12 : IF famacha IS bege AND opg IS baixo THEN situacaoAnimal IS atencao;
	
	RULE 13 : IF famacha IS begeClaro AND opg IS  alto THEN situacaoAnimal IS muitoMal;
	RULE 14: IF famacha IS begeClaro AND opg IS  medio THEN situacaoAnimal IS muitoMal;
	RULE 15 : IF famacha IS begeClaro AND opg IS baixo THEN situacaoAnimal IS mal;// pois se o olho esta mal e OPG baixo algo esta errado
	RULE 16 : IF opg IS alto THEN situacaoAnimal IS muitoMal;
	RULE 17 : IF famacha IS begeClaro THEN situacaoAnimal IS muitoMal;
	RULE 18 : IF famacha IS bege THEN situacaoAnimal IS mal;
END_RULEBLOCK

END_FUNCTION_BLOCK

